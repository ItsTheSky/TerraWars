plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.14'
    id 'io.freefair.lombok' version '8.13.1'
    id 'com.gradleup.shadow' version '8.3.3'
}

group = 'net.itsthesky'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "CodeMC"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
}

dependencies {
    paperweight.paperDevBundle('1.21.4-R0.1-SNAPSHOT')

    // MiniMessage
    implementation 'net.kyori:adventure-text-minimessage:4.19.0'

    // CommandAPI
    implementation 'dev.jorel:commandapi-bukkit-shade-mojang-mapped:9.7.0'

    // GSON
    implementation 'com.google.code.gson:gson:2.10.1'

    // IF
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.19'

    // FAWE
    implementation platform("com.intellectualsites.bom:bom-newest:1.52")
    compileOnly "com.fastasyncworldedit:FastAsyncWorldEdit-Core"
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit") {
        transitive = false
    }
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

tasks.assemble {
    dependsOn(tasks.reobfJar)
}

shadowJar {
    archiveFileName.set("${project.name} v${project.version}.jar")

    relocate 'dev.jorel.commandapi', 'net.itsthesky.commandapi'
    relocate 'com.github.stefvanschie.inventoryframework', 'net.itsthesky.inventoryframework'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

compileJava.options.encoding = 'UTF-8'